import ColourOracle;
import SimT;
import utils;

numOfCols := 4;

// Simulation of q. walk hamiltonian at a time t
def Ham[n:!N](
  const G_or: !N x uint[n]->lifted uint[n],
  t:!R,
  node_reg:uint[n],
  neighbour_anc:uint[n],
  err_anc:B){
  
  for col in [0..numOfCols){
    // Perform first V_c oracle
    [neighbour_anc, err_anc] := V(G_or, col, node_reg, neighbour_anc, err_anc);

    // Simulation of T operation
    [node_reg, neighbour_anc, err_anc] := sim_T(t, node_reg, neighbour_anc, err_anc);

    // Perform second V_c oracle
    [neighbour_anc, err_anc] := V(G_or, col, node_reg, neighbour_anc, err_anc);
  }

  return (node_reg, neighbour_anc, err_anc);
}

// Making calls to quantum walk a number of times
def BWT[n:!N](const G_or: !N x uint[n] ->lifted uint[n], steps:!N, t:!R, dumpState:!B){
  // Start a in the Entrance state
  node_reg := 1:uint[n];
  neighbour_anc := 0:uint[n];
  err_anc := 0:B;

  for step in [0..steps){
    print(step + 1);
    [node_reg, neighbour_anc, err_anc] := Ham(G_or, t, node_reg, neighbour_anc, err_anc);
  }

  if dumpState{
    dump();
  }

  node_reg := measure(node_reg);
  measure(neighbour_anc);
  measure(err_anc);
  return node_reg;
}